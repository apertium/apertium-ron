DELIMITERS = "<.>" "<..>" "<...>" "<!>" "<?>" "<¶>" sent ;
SOFT-DELIMITERS = "<,>" ;
SUBREADINGS = LTR ;

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

SETS

LIST N = n ;
LIST Adj = adj ;
LIST Pr = pr ;
LIST Det = det ;
LIST Adv = adv ;
LIST Prn = prn ;
LIST Vbavea = vbavea ;
LIST V = vblex ;
LIST Vbser = vbser ;
LIST Vaux = vaux ;
SET Pp = (vblex pp) | (vbmod pp) | (vaux pp) | (vbser pp) | (vbhaver pp) | (vbdo pp) ;

LIST Sg = sg ;
LIST Pl = pl ;
LIST Sp = sp ;
LIST M = m ;
LIST F = f ;
LIST Nt = nt ;
LIST Mf = mf ;
LIST Mfn = mfn ;
LIST Mn = mn ;
LIST Fn = fn ;

LIST Nom = nom ;
LIST Dg = dg ;
LIST Pri = pri ;
LIST Prs = prs ;
LIST Inf = inf ;

LIST Să = ("să") ;

SECTION

# Entries which are either N or Adj
SELECT N IF (0 N) (0 Adj) ;

# Entries which are either Pp or Adj
SELECT Pp IF (0 Pp) (0 Adj) (-1 Vbavea) ;
SELECT Pp IF (0 Pp) (0 Adj) (-2 Vbavea) (-1 ("fi") + Pp) ;
REMOVE Pp IF (0 Pp) (0 Adj) ;

# Mai (month or adverb)
REMOVE N IF (0 ("mai")) (-1 N) (1 Adj) ;
REMOVE N IF (0 ("mai")) (-1 V) (1 Adv) ;
REMOVE N IF (0 ("mai")) (-1 ("cel")) (1 Adj) ;

# A (prep or vbavea)
SELECT Vbavea IF (0 ("a")) (1 Pp) ;

# Conjunctiv
SELECT Prs IF (0 Prs) (0 Pri) (-1 Să) ;
SELECT Prs IF (0 Prs) (0 Pri) (-2 Să) (-1 Adv);
SELECT Prs IF (0 Prs) (0 Pri) (-2 Să) (-1 Prn);
SELECT Prs IF (0 Prs) (0 Pri) (-3 Să) (-2 Adv) (-1 Prn);
REMOVE Prs IF (0 Prs) (0 Pri) ;

# Vrea
REMOVE V IF (0 V) (0 Vaux) (1 Inf) ;
REMOVE V IF (0 V) (0 Vaux) (1 Să) (2 Prs) ;
REMOVE Vaux IF (0 V) (0 Vaux) ;

# Avea
REMOVE V IF (0 V) (0 Vbavea) (1 Pp) ;
REMOVE V IF (0 V) (0 Vbavea) (1 Să) (2 Prs) ;
REMOVE Vbavea IF (0 V) (0 Vbavea) ;

# Remove dative/genitive by default
REMOVE Dg IF (0 Nom) (0 Dg) ;



# Resources:
# http://visl.sdu.dk/cg3.html
# http://groups.google.com/group/constraint-grammar
# http://kevindonnelly.org.uk/2010/05/constraint-grammar-tutorial/
# http://wiki.apertium.org/wiki/Constraint_Grammar
# http://wiki.apertium.org/wiki/Apertium_and_Constraint_Grammar

